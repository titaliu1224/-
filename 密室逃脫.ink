點擊文字選項以進行遊戲
* [開始遊戲] 
    ->Game_Start

=== Game_Start ===

VAR time = 1

陽光灑落，鬧鐘鈴鈴鈴的響。
+   [拿起手機] -> Take_Phone
*   再睡一下[]...
    我翻了個身，拉起被子將自己裹得嚴實，好像這樣就聽不見那惱人的鈴聲似的。
    今日安靜的出奇，我很快就又進入夢鄉。
    ~time = time + 1
    -> Game_Start

=== Take_Phone ===

「吵死了。」
我伸手想熟練的按掉鬧鐘，卻發現介面長得不太一樣。
這應是 Android 的介面，而我是眾所皆知的果粉，早已使用 iPhone 多年。
這不是我的手機！

 * [試著解鎖] 
    將睡意拋到千里外，<>
    -> unlock
 * [放著不管] -> Do_Not_Take_Phone

= unlock
    我滑開了螢幕鎖定畫面。
    這支手機沒有上鎖，但滑開後的介面完全不是我所熟悉的 Android 手機。
    手機主畫面（？）空無一物，是一片純淨的黑，除了正中央有個同樣是黑色的 icon。

    * [點開它] 
        -> open
    * [無視它]
        滑了又滑，這支手機好像沒有別的應用程式了。
        -> no_open

= open
    ~time = time + 1
    伸手點擊，彈出的是一個類似感謝卡的圖片：
    <i>「感謝您參與我們的計畫，安擎先生，我們已依照約定將款項送至指定帳戶。</i>
    <i>請享受我們為您準備的盛宴，節目將於下午五點開始。」</i>
    右下角署名：歐亙企業
    
    * [查看時間]
        手機上並沒有任何顯示時間的地方。 
        -> open
    * 什麼盛宴？
        這麼想來，我也有些餓了。 
        -> open
    * 什麼節目？
        聽起來毛毛的，我應該要趕緊採取行動了。 
        -> open
    * [環顧四周]
        -> See_Surrounding

= no_open
    + [環顧四周]
        -> See_Surrounding
        
=== Do_Not_Take_Phone ===

好可怕，我還是不要帶著這來路不明的東西吧。
我將手機擱置在床頭並坐起身。

+ [環顧四周] -> See_Surrounding

//--------------------------查看四周-----------------------------//

=== See_Surrounding ===
// 遊戲結束
{time > 40:
    -> Bad_Ending
}

// 過半提醒
{time > 20: 
    {not See_Phone:
        { Take_Phone.unlock:
            -> Phone_vibration
        }
    }
}
<br>
- 我環顧四周，這裡被整理的整齊——只有一張床和兩扇門，要不整齊也難吧。
天花板上有日光燈，但因牆壁、門、地板和床都是一片純白，所以自玻璃窗透進來的陽光能在房內反射，看不出來燈是否是開啟狀態。

+ [查看床鋪] -> See_Bed
+ [查看天花板] -> See_Ceiling
+ [查看地板] -> See_Floor
+ [查看牆壁] -> See_Wall
+ [查看左邊的門] -> See_Left_Door
+ [查看右邊的門] -> See_Right_Door
+ [查看窗戶] -> See_Window
+ [查看隨身物品] -> See_Things

=== Phone_vibration ===
    嗡嗡——
    口袋中的手機震動了兩下。
    + [拿起來看] -> See_Phone
    
=== See_Phone ===
手機中有一則陌生通知，似乎是來自那唯一的黑色 app。
+ [查看訊息]
    <i>「提醒您，宴會將於半小時後開始，屆時會有專人帶您到宴會會場，請您在這段時間內完成洗漱。</i>
    <i>我們期待您的到來。」
    ++ 半小時？
        時間過得這麼快嗎？看來我得快一點了。
        -> See_Surrounding
    ++ 宴會？專人？那很好啊！
        等到那時就能請他們解釋一切原委，並告訴我出去的辦法了吧！
        +++ [坐下靜待時間過去]
            我回到床上坐下，看著這個房間。
            我依舊沒有頭緒我為何身在這裡，也不知道是否該逃跑。
            或許等等的宴會會告訴我答案吧。
            ......
            -> Bad_Ending
        +++ [去廁間洗漱整理儀容]
            -> See_Left_Door
        +++ [繼續探索房間]  
            -> See_Surrounding

=== See_Bed === // 鑰匙在枕頭裡
純白的被褥，純白的枕頭，純白的床單……我要是住在這裡一定會瘋掉。
+ [查看枕頭] ->pillow
+ [查看被子] -> comforter
+ [查看床單] -> sheet
* {Do_Not_Take_Phone} [嘗試拿起手機] -> Take_Phone.unlock
+ [什麼都不看] -> See_Surrounding

= pillow // 會掉鑰匙
    就是個枕頭。
    + [敲打它] 
        伸出拳頭朝它一頓猛揍……消耗體力後感覺更餓了。
        ~time = time + 1
        -> pillow
    + [晃動它] 
        我拿起來搖了搖。
        ~time = time + 1
        -> shake_pillow
    + [不理它] -> See_Bed
    
    = shake_pillow
        {key: -> pillow}
        {~ -> pillow | -> key }

= key // 房間主鑰匙
    一把鑰匙掉了出來，在床鋪上翻了兩圈。

    * 我將鑰匙收好，以備不時之需[]。
        -> See_Bed

= comforter // 不會有事發生
    蓬蓬的被子，在開著空調的房間裡可以說是天堂。
    
    + [抖動它]
        我揮動手臂，使勁抖了兩下。
        ~time = time + 1
        -> comforter
    + [鋪平它]
        將它鋪平後我伸手撫平它的摺痕。
        ~time = time + 1
        -> comforter
    + [什麼都不做]
        -> See_Bed

= sheet // 啥都沒有
    我走下床，掀開被子。
    就是個床單。
    ~time = time + 1
    -> See_Bed
    
=== See_Ceiling === // 啥都沒有

抬頭，就是水泥一般的天花板，但被漆成白色，房間中央有方形的日光燈，燈光微弱。
+ 有辦法摸到它嗎？
    伸長手臂……
    怎麼可能呢。
    ~time = time + 1
    -> See_Ceiling
+ 沒啥好看的。
    -> See_Surrounding

=== See_Floor === 

白色磁磚地，好像蒙了一層灰。

* 到底有沒有掃地啊？
    看著因我的一舉一動而揚起的灰塵反射著陽光，我邊小聲嘀咕抱怨。
    -> See_Floor
+ [趴到地上] 
    ->lay
+ 沒啥好看的。 
    -> See_Surrounding

= lay
    我趴到地上，朝各處掃視。
    
    + 像個智障一樣……[]我站起身。
        -> See_Floor
    + 掃視床底[]，
        ~time = time + 1
        -> under_bed
    
= under_bed
    {photo: <>除了灰塵外，已經沒有東西了。}
    {photo: ->lay}

    <>發現了一張小卡。
    
    * [鑽進去撿起來]
        盯著床底更厚的灰塵，我硬著頭皮鑽進去，伸長手將小卡拿了出來。
        -> photo
    * { See_Wall.stick } [用桿子撈出來]
        伸長桿子，我慢慢的把小卡撈出來，剛剛有找到這根桿子實在是太好了。
        -> photo
    * 好髒。
        不得不說，床底下的灰塵更厚了是要我怎麼進去？
        -> lay

= photo
    是我和哥哥小時候的照片。
    照片中的我們頂著太陽，穿著同套衣服，對鏡頭比著大大的勝利手勢。
    ~time = time + 1
    
    * 哥哥小時候總是帶著我到處玩……
        某個周末，我們去附近的公園踢球，我為了接飛得太高的球而因此跌倒擦傷膝蓋，紅色的血液讓我哭了出來，哥哥拉著我跑到小學的保健室想找校護阿姨，卻因假日無人留守，兩人便在無人的學校探險。
        又曾有次放學，說著要走捷徑回家，走著走著，30分鐘後我們又回到了原點。
        國中時，每日早晨也都是哥哥邊罵著髒話邊叫我趕緊起床，早餐已經買好放在桌上，要我拿了去學校吃……
        ** 這張照片是哥哥老師送我們的禮物。
            自幼父母雙亡的我們被轉送到親戚家，由於不是親生孩子，親戚總是對我們視若無睹。
            哥哥從小就擔起了爸爸的責任，我知道我虧對了他，所以他才會在上大學後變成了另一個人。
            是我的錯呀。
        ~time = time + 1
        -> See_Floor
    
    * 算了，回憶這些幹嘛，都過去了。
        -> See_Floor

=== See_Wall ===

{ stick: 牆上已經被我打出一個大洞了……}
{ stick: -> See_Surrounding}

我沿著房間走一圈，就是牆壁，毫無坑洞的那種。

+ [敲敲看] -> knock
+ 沒啥好看的 -> See_Surrounding

= knock
~time = time + 1
咚！咚！咚！
{~ ->See_Wall | <>扣！有一處聲音不太一樣。-> special_wall}

= special_wall
+ [用力打]
    -> hit_wall
+ 算了，安靜一點的好[]，太吵而驚動到什麼人就糟了。
    -> See_Surrounding
    
= hit_wall  
    ~time = time + 1
    我使勁打它、踢它，發出了巨大聲響。
    {||| -> stick}
    -> special_wall
    
= stick // 獲得伸縮桿
    匡噹——
    「牆」被敲出了一個洞。
    裡頭有一支像是用來掛門簾一樣的伸縮桿，和我的前臂差不多長。
    
    + 將桿子拿在手上[]，我繼續探索周圍。
        -> See_Surrounding

=== See_Left_Door ===

門輕易的被推開，是小間且簡陋的廁所。

* 「至少有打掃……」[]我邊自言自語邊走進去。
    雖然除了馬桶和洗手台外，這裡什麼都沒有，但還是蠻乾淨的，白色的地板上也見不著灰塵。
    -> See_Left_Door
* 先上個廁所吧[]。
    ~time = time + 1
    -> pee
+ [查看馬桶]
    雖然百般不願意，還是看一下吧。
    ~time = time + 1
    -> toilet
+ [查看天花板]
    ~time = time + 1
    -> ceiling
+ 沒啥好看的[]。
    我退出廁所並帶上門。
    ->See_Surrounding

= pee
打從一早起來就想上廁所了，好險這裡的設備還算完善。
所以說這個房間到底是哪裡啊？

+ [沖馬桶]
    -> flush
    
+ [別沖吧]
    還是別沖的好，吵到誰就完蛋了。
    -> See_Left_Door
    
= flush // 沖水會浮現螺絲起子
    ~time = time + 1
    我按下沖水把手，水嘩啦嘩啦的沖過，聲音迴盪在整個廁間。
    唉呀，好像有點吵。
    -> See_Left_Door
    
= toilet
就是個普通的白色傳統馬桶，沒有什麼自動升降馬桶蓋或是溫暖坐墊的功能。

+ [沖水]
    -> flush
+ [打開水箱蓋子]
    我打開水箱蓋子檢查，<>
    ->waterbox
+ [沒啥好看的]
    -> See_Left_Door

= waterbox
    { screwdriver:
        水箱正在填充新的水。
        -> toilet
    }
    { not flush: 
        裡頭就是水，還因為加了清潔劑而有點混濁，所以有種深不見底的錯覺。
        -> toilet
    }
    {flush:
        水箱因剛剛沖過馬桶所以正在填充水，水箱底部有個螺絲起子，看起來早已生鏽。
        
        + [拿起螺絲起子] 
            雖然要把手伸進馬桶中是百般不願意，但水箱應該是蠻乾淨的吧？
            -> screwdriver
        + [不理他]
            不論是可疑的螺絲起子還是馬桶水箱，都不是讓人想觸碰的東西。
            -> toilet
    }
    
= screwdriver
    ~time = time + 1
    我拿起螺絲起子，並將它身上的水甩掉。
    因為長期處在潮濕的環境，它生鏽得像是一擰螺絲便會自己碎掉，不知道是誰放進水箱裡的呢？
    
    + 「一定是以前也在這裡居住過的人。」
    即便還不清楚這裡是哪裡，以前曾經有誰居住過也不清楚，但能夠找到一些道具實在是讓我安心了不少。
    -> toilet

= ceiling
    天花板像是用塑膠板拼接而成，而角落有個抽風口。
    + [敲敲看天花板]
        要怎麼敲呢？
        ++ [試圖爬上馬桶敲]
            我蓋上馬桶蓋，小心翼翼的踩上並伸出手。
            還是碰不到啊......
            早知道小時候多打一些籃球、多跳一些繩，才能長得高一點。
            ~time = time + 1
            -> ceiling
        ++ {See_Left_Door.screwdriver}[拿螺絲起子戳]
            我蓋上馬桶蓋，小心翼翼的踩上並伸出手。
            這個起子還是不夠長......
            ~time = time + 1
            ->ceiling
        ++ {See_Wall.stick}[拿桿子敲]
            我舉起方才找到的桿子，伸長它好讓我能頂到天花板。
            扣！扣！扣！
            天花板裡頭是空心的。
            ~time = time + 1
            +++ [破壞他]
                他裡面一定有什麼端倪！
                我用力拿桿子戳其中一片塑膠板。
                ~time = time + 1
                -> knock_ceiling
            +++ [不理他]
                誰會想要對天花板施展暴力啊？
                雖然我也才破壞過牆壁就是了。
                ->ceiling
                
        +{break_ceiling}[查看天花板內部]
            -> break_ceiling
        
                
    + [查看抽風口]
        嗯......就是很普通的抽風口，一打開廁所的燈，它便會嗡嗡作響開始運作。
        ++[對著抽風口求救]
            喂！！有人嗎！！！
            我朝它大喊，但沒有什麼反應，只有回音迴盪在整個廁間。
            ~time = time + 1
            -> ceiling
        ++[對著抽風口大罵]
            幹！！你們到底是三小東西！！抓我走是什麼意思！
            是腦袋有洞需要我幫你們補起來是不是！
            ......罵完心情舒坦了一些。
            ~time = time + 1
            -> ceiling
        ++ [沒什麼好看的]
            -> ceiling
            
    +沒啥好看的[]。
        -> See_Left_Door

    = knock_ceiling
        + 碰！[]
            -> pong
        + 放棄吧
            -> ceiling

    = pong
        {~  |  |  | -> break_ceiling }
        巨大聲響迴盪在整個房間中。
        我有點害怕有誰發現了我的怪異行為。
        ~time = time + 1
        ->knock_ceiling
       
    = break_ceiling
        那片塑膠板終於被我推開，天花板裡頭非常的暗。
        *{Take_Phone.unlock} [用手機照亮]
            對了，我還有那個奇怪的手機啊！
            好險它仍保有手機的基本功能－－手電筒。
            我打開它，對著天花板裡面四周檢查。
            裡面有一些複雜的管線......
            還有一個......盒子？
            **[拿出來看]
                伸長手拿出這個盒子，<>
                ~time = time + 1
                -> key_box
        + 離開吧[]。
            -> ceiling
    = key_box
        它是一個精緻的木盒，感覺是用來裝首飾的。
        它被鎖住了。
        盒子中間有個金色的金屬鎖，有些掉漆與生鏽，可能是銀質的。
        *{See_Bed.key}[用鑰匙開鎖]
            -> key
                
        +[打不開...]
            試著使用暴力拉開，它依舊不為所動......
            只好先收起來了。
            -> ceiling
    = key
        拿出在枕頭下找到的鑰匙，很順利的轉開了鎖。
        木盒中有著柔軟的凹槽，其中放著的是一把粗糙的鑰匙。
        + 用鑰匙拿鑰匙......[]這還真是有點荒謬。
            不過它看起來很重要，先收著吧。
            ~time = time + 1
            -> ceiling
            
=== See_Right_Door ===
一面木門，就和一般辦公室中會出現的一樣。只是上頭沒有任何指示牌或是能看見外面情況的小玻璃窗。
+ [嘗試打開]
    我握緊門把，下壓......門被鎖住了。
    這不是理所當然的嗎
    ~time = time + 1
    -> See_Right_Door
+ {See_Bed.key}[用枕頭下發現的鑰匙解鎖]
    從口袋中拿出剛剛發現的鑰匙。
    插不進去耶，是我的角度不對嗎？
    ~time = time + 1
    -> See_Right_Door
+ {See_Left_Door.key}[用木盒中的鑰匙解鎖]
    從剛剛在廁間天花板發現的木盒中取出鑰匙，我小心翼翼的把他對準鎖孔。
    喀擦。
    是鎖被轉開的聲音。
    ++ 將門推開[]，外面是一條長廊，盡頭是個叉路，兩側牆壁只有消防栓等工具，沒有任何路標或是其他房間。
        天花板的日光燈是冷冷的白色，有種身在辦公室的錯覺。
        走廊上非常安靜，沒有人來往。
        +++ [走出去]
            -> Good_Ending
        
        +++ [回到房間待著]
            「外頭有點奇怪啊......」
            我嘀咕著，邊輕輕關上門，以免有人發現我試圖亂跑。
            只好等宴會開始了。
            -> Bad_Ending
+ [沒什麼好看的]
    -> See_Surrounding
            
=== See_Window ===
窗戶高掛在牆上，從我的角度只能看到天空。
+ [跳起來看外頭]
    我在原地使勁的跳，隱約能見到外面是一片田地，沒有高樓大廈。
    ......好累。
    ~time = time + 1
    -> See_Window
+ [爬到床上跳跳看]
    我爬到床上使勁的跳，彈簧床讓我能更順利的看見外頭。
    外面就是一片農田，沒有高樓、沒有車水馬龍的街道。
    做為一個城市小孩，這裡讓人感到很陌生。
    ~time = time + 1
    -> See_Window
+ [伸手去摸]
    我掂起腳尖、伸長手臂。
    {necklace: 
        窗框上除了滿滿的灰塵外，沒有別的東西了。
        ~time = time + 1
        -> See_Window
    }
    窗框上滿是灰塵，不過我摸到了觸感不一樣的東西。
    ** [拿起來看]
        -> necklace
+ [沒什麼好看的]
    -> See_Surrounding

= necklace
    這是一個金質的項鍊。
    細細的金鍊上有著四葉幸運草的裝飾，很是典雅。
    不知道是誰的呢？
    ~time = time + 1
    -> See_Window
    
=== See_Things ===
~temp things = 0
我身上的東西有：<>
{Take_Phone.unlock: 
    來路不明的手機<>
    ~things++
}
{See_Bed.key:
    {things > 0 : <>、<>}
    枕頭下發現的鑰匙
    ~things++
}
{See_Floor.photo:
    {things > 0 : <>、<>}
    床底下的照片
    ~things++
}
{See_Wall.stick:
    {things > 0 : <>、<>}
    牆壁中的桿子
    ~things++
}
{See_Left_Door.screwdriver:
    {things > 0 : <>、<>}
    馬桶水箱中的螺絲起子
    ~things++
}
{See_Left_Door.key_box:
    {things > 0 : <>、<>}
    廁所天花板中的木盒
    ~things++
}
{See_Left_Door.key:
    {things > 0 : <>、<>}
    木盒中的鑰匙
    ~things++
}
{See_Window:
    {things > 0 : <>、<>}
    窗框上的項鍊。
    ~things++
}

{things == 0:
    ......我身上什麼都沒有。
    有人偷了我的東西？？
}

-> See_Surrounding
        
=== Good_Ending ===
我鼓起勇氣往前走，腳步聲和日光燈微微的嗡嗡聲是這裡唯一的聲音。
前方，不知道還有什麼在等著我？
<b>遊戲結束</b>
-> END

=== Bad_Ending ===
叩叩叩！敲門聲自右邊的門傳來。
隨即，兩名身著西裝、帶著墨鏡的男子直朝我走來。
「安擎先生，宴會即將開始了，我們會帶您到宴會會場」其中一名男子說。
* [試圖反抗]
    「你...你們要幹嘛......不要過來！」我慢慢挪後身子，邊舉起手想擋住他們。
    他們似乎不太開心，邊說著請我配合邊把我架了出去。
* [跟他們走]
    「好的，麻煩你們了。」我頷首，他們一錢一後的帶我出去。

- 房間外頭是個走廊，天花板上的日光燈是冷冷的白色。
跟著西裝男走很快就來到了類似餐廳的地方。
餐廳中有很多組桌椅，但只有其中幾桌擺了各式的食物，沙拉、主餐、甜點，應有盡有。
其中一桌坐了一名男性，身著直條紋西裝，看來已等候多時。
* [跟他對話]
    正當我想說話時，他先我一步開口：「請坐，挑您喜歡的吃」。
    話落，餐廳中響起悠揚的音樂，但此時滿腹疑問的我才沒心情聽音樂吃美食。
    ** 「你們是誰？」[]我問道。
        桌前的男人向西裝男駛了眼色，西裝男立刻拿出了幾張A4紙。
    *** 從哪拿出來的紙啊？？
        我忍不住在心裡吐槽。
    *** ...
- 襯衫男翻閱了一下，將其遞到我面前。
「容我和您介紹一下，敝姓簡，我是您這次合約的專員。」
「我們是歐亙企業——雖然並不是合法的那種。」說到這，他自嘲的笑了笑。
「我們以販售人體器官聞名，顧客將器官或身體交給我們，而我們付錢給客戶。」
「這比起器官捐贈來說，可是一筆非常划算的交易兼公益活動——你知道的，光靠捐贈根本無法滿足所有需要健康器官的人，若有我們和我們顧客的幫忙，一切都會順利許多。」
* 這人到底在說什麼？？
    這樣是違法的吧？為什麼他要和我說這個？？
* 所以我是他們口中的「顧客」嗎？
    ......為什麼？
    ** 錢去了哪裡？
        若我真的是顧客，我應當獲得一筆錢。
        但我並不是愛錢如癡的人，即便再怎麼樣都不可能做這種交易。
        除非有人為了錢陷害我？
- 「您不懂嗎？」
「您在獲得一大筆錢的同時，也拯救了另一個人的性命！」
* 他瘋了吧？[]我在心裡暗自咒罵。
    「所以您當時聯絡了我們，和我們簽了約。」男人將桌上的白紙再朝我推了幾公分。
* 「所以我簽了約嗎？」[]我問。
    「是的，沒錯。這就是您當時簽的約。」男人將桌上的白紙再朝我推了幾公分。
- 「您的合約是賣出您一側的肺葉，這並不會對您的健康造成多大影響。」
「而款項與匯款資訊都寫在合約中了，請不要擔心，我們已將約定的款項匯至您的帳戶中了。」
* [查看收款資訊]
這個收款帳號不是我的啊......？
不對，有點眼熟。
是哥哥的？
認知到這個事實後，男人喋喋不休的在講些什麼我一個字都沒聽進去。
仔細回想起來，好像真有些和哥哥、錢有關的事。
++ 哥哥......哥哥......
啊，哥哥出了車禍！
當時接到消息後我馬上趕去了醫院，醫生卻說需要一大筆錢來做手術。
我急著各處借款，終是湊齊了金額，然而根本沒有能力還得起。
直到某夜我喝得有點醉，見著了歐亙的廣告，便找了他們簽了約。
+++ 是我自己做的嗎......
既不是有人害我，也不是什麼戲劇化的過程，僅僅是我叫來了人，簽下自己的名字而已。
<br>
- 眼前的男人笑得親切，我卻覺得他絕非善類。
+ [接受事實]
    「好吧，我知道了。」我打自進餐廳以來，第一次開口講話。
    「你們給的金額也算是合理，我接受你們的交易。」
    男人朗朗大笑「我就喜歡您這種人！請您享用美食吧，我不吵您了，我兩個小時後會回來向您說明後續事宜，您就先沉澱一下心情吧！」
+ 「能夠取消合約嗎？」[]我小心翼翼的問道。
    「不好意思，是我聽錯了嗎？」男人偏頭，臉上的微笑變得有些僵硬。
    ++ 「我想取消合約。」
        我重複了剛剛說的話，男人微慍的臉色讓我有些害怕。
        「請您享用美食吧，我兩個小時後會回來向您說明後續事宜。您就先沉澱一下心情吧。」
    ++ 「呃......沒事。」
        男人恢復了剛剛的笑容，起身道「請您享用美食吧，我不吵您了。我兩個小時後會回來向您說明後續事宜，您就先沉澱一下心情吧！」
- 男人起身離開了餐廳，留下西裝男守在餐廳門口。

<b> 遊戲結束</b>
    
-> END
    
